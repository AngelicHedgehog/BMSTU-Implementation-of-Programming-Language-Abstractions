*$FROM src/DYN/DYN_Code.ref
*$FROM src/DYN/DYN_Expr.ref
*$FROM src/DYN/DYN_LocalVars.ref
*$FROM src/DYN/DYN_BoolExpr.ref
*$FROM LibraryEx
$EXTERN DYN_Code, DYN_Expr, DYN_LocalVars, DYN_BoolExpr, Map;

/**
  <DYN_Statement
      (t.Expr "=" t.Expr)
    | (call t.Expr t.Expr*)
    | (mcall t.Expr s.Name t.Expr*)
    | (return t.Expr)
    | (if t.BoolExpr e.Code)
    | (if t.BoolExpr e.Code else e.Code)
    | (while t.BoolExpr e.Code)
    | (asm s.ANY+)
    | (block t.LocalVars e.Code)
    | (init t.ObjectPtr s.Name)
    | (t.Expr ":-" t.Expr)
    | (gc-alloc t.Expr s.Name)
    | (ref-return t.Expr)
  > == t.Statement
*/
$ENTRY DYN_Statement {
  (t.ObjectPtr "=" t.Expr) = (<DYN_Expr t.ObjectPtr> "=" <DYN_Expr t.Expr>);
  (call t.Expr e.Args) = (call <Map DYN_Expr t.Expr e.Args>);
  (mcall t.Object s.Method e.Args) = (mcall <DYN_Expr t.Object> s.Method <Map DYN_Expr e.Args>);
  (return t.Expr) = (return <DYN_Expr t.Expr>);
  (if t.BoolExpr e.CodeT else e.CodeF)
    = (if <DYN_BoolExpr t.BoolExpr> <DYN_Code e.CodeT> else <DYN_Code e.CodeF>);
  (if t.BoolExpr e.Code) = (if <DYN_BoolExpr t.BoolExpr> <DYN_Code e.Code>);
  (while t.BoolExpr e.Code) = (while <DYN_BoolExpr t.BoolExpr> <DYN_Code e.Code>);
  (block t.LocalVars e.Code) = (block <DYN_LocalVars t.LocalVars> <DYN_Code e.Code>);
  (init t.ObjectPtr s.Name) = (init <DYN_Expr t.ObjectPtr> s.Name);
  
  /* TODO: подсчет ссылок нужен бы тут */
  (t.Expr ":-" t.Expr) = ((L <DYN_Expr t.Expr>) "=" <DYN_Expr t.Expr>);
  (gc-alloc t.Expr s.Name) = (<DYN_Expr t.Expr> "=" (call allocBlock__ s.Name));
  (ref-return t.Expr) = (return (L <DYN_Expr t.Expr>));
  
  e.Other = e.Other
}
