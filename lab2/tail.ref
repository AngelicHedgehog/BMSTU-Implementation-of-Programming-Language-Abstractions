$EXTERN LoadFile, ArgList, Map, DelAccumulator;

$ENTRY Go {
    /* пусто */ = <Main <ArgList>>;
}

Main {
    (e.ProgPath) ('-' e.LinesCount) e.Files
        , <atoi e.LinesCount> : s.LinesCount
        , e.Files : {
            /* пусто */ = (<LoadFile 0>);
            e._ = <Map {e.File = (<LoadFile e.File>);} e.Files>;
        } : e.FilesLines
        = 
            <Map {
                e.FileLines =
                    <Map Prout <DelAccumulator
                        <Last s.LinesCount e.FileLines>>>;
            } e.FilesLines>;

    (e.ProgPath) e.Files = <Main (e.ProgPath) ('-10') e.Files>;
    e._ =
        <Putout 0 "Error: Main: wrong arguments format.">
        <Exit 1>;
}

$ENTRY ctoi {
    s.Char
        , <Sub <Ord s.Char> <Ord '0'>> : s.Digit
        , <Compare s.Digit <Sub 0 1>> <Compare s.Digit 10> : {
            '+-' = s.Digit;
            e._ = 
                <Putout 0 "Error: ctoi: digit must be between 0 and 9.">
                <Exit 1>;
        };
    e._ =
        <Putout 0 "Error: ctoi: wrong arguments format.">
        <Exit 1>;
}

$ENTRY atoi {
    e.Other s.PreLast s.Last = <Add <Mul <atoi e.Other s.PreLast> 10> <ctoi s.Last>>;
    s.Once                   = <ctoi s.Once>;
    e._ =
        <Putout 0 "Error: ctoi: wrong arguments format.">
        <Exit 1>;
}

